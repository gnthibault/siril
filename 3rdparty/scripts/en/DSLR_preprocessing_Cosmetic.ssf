############################################
# Script for Siril 0.9
# January 2019
# (C) Cyril Richard
# DSLR_preprocessing+CC v1.0
########### PREPROCESSING SCRIPT ###########
# Script for DSLR color camera preprocessing
# needs 4 sets of RAW images in the working
# directory, within 4 directories:
# biases, flats, darks and lights 
# In addition to DSLR_preprocessing it
# applies an automatic cosmetic correction
#
############################################

#build master-bias
cd biases
convertraw bias_
stack bias_ rej 3 3 -nonorm
cd ..

cd flats
#preprocess flats
convertraw flat_
preprocess flat_ -bias=../biases/bias_stacked

#build master-flat
stack pp_flat_ rej 3 3 -norm=mul
cd ..

#build master-dark
cd darks
convertraw dark_
stack dark_ rej 3 3 -nonorm
cd ..

#preprocess lights
cd lights
convertraw light_
preprocess light_ -dark=../darks/dark_stacked -flat=../flats/pp_flat_stacked -cfa -equalize_cfa

#Automatic cosmetic correction
seqfind_cosme_cfa pp_light_ 3 3

#debayer images
preprocess cc_pp_light_ -debayer -stretch

#align lights
register pp_cc_pp_light_

#stack calibrated lights
stack r_pp_cc_pp_light_ rej 3 3 -norm=addscale

#That's a workaround here to save result in another place.
#These lines are not mandatory
#load image in memory
load r_pp_cc_pp_light_stacked

#and save it at root
cd  ..
save result
close
