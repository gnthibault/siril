############################################
# Script pour Siril 0.9.9
# Juin 2018
# (C) Cyril Richard
# Adapté, modifié et traduit par Colmic
# 
# 
# Pré-traitement APN
# Sans les darks
# 
# Ce script a besoin de 3 dossiers pour fonctionner
# à installer dans votre dossier de travail de Siril :
# offsets, flats, brutes
#
############################################

#Création du master-offset
cd offsets
convertraw offset_
stack offset_ rej 3 3 -nonorm
cd ..

cd flats
#Pré-traitement des flats
convertraw flat_
preprocess flat_ -offset=../offsets/offset_stacked.fit

#Création du master-flat
stack pp_flat_ rej 3 3 -norm=mul
cd ..

#pré-traitement des brutes
cd brutes
convertraw brute_
preprocess brute_ -offset=../offsets/offset_stacked.fit -flat=../flats/pp_flat_stacked.fit -debayer

#Alignement des brutes
register pp_brute_

#Empilement des brutes calibrées
stack r_pp_brute_ rej 3 3 -norm=addscale

#Chargement de l'image résultante en mémoire
load r_pp_brute_stacked.fit

#Sauvegarde de l'image à la racine du dossier de travail
cd  ..
save resultat.fit
close