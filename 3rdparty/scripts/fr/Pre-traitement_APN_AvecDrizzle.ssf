############################################
# Script pour Siril 0.9
# Mars 2020
# (C) Cyril Richard
# Adapté, modifié et traduit par Colmic
# Pre-traitement_APN v1.6
# 
# Pré-traitement pour APN avec option Drizzle 2x
# 
# 
# Ce script a besoin de 4 dossiers pour fonctionner
# à installer dans votre dossier de travail de Siril :
# offsets, darks, flats, brutes
#
############################################

#Création du master-offset
cd offsets
convertraw offset_
stack offset_ rej 3 3 -nonorm
cd ..

cd flats
#Pré-traitement des flats
convertraw flat_
preprocess flat_ -bias=../offsets/offset_stacked

#Création du master-flat
stack pp_flat_ rej 3 3 -norm=mul
cd ..

#Création du master-dark
cd darks
convertraw dark_
stack dark_ rej 3 3 -nonorm
cd ..

#pré-traitement des brutes
cd brutes
convertraw brute_
preprocess brute_ -dark=../darks/dark_stacked -flat=../flats/pp_flat_stacked -cfa -equalize_cfa -debayer -stretch

#Alignement des brutes avec option drizzle 2x
register pp_brute_ -drizzle

#Empilement des brutes calibrées
stack r_pp_brute_ rej 3 3 -norm=addscale -output_norm -out=../resultat

cd ..
close