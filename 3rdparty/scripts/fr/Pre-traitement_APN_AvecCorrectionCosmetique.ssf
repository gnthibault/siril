############################################
# Script pour Siril 0.9
# Mars 2019
# (C) Cyril Richard
# Adapté, modifié et traduit par Colmic
# Pre-traitement_APN v1.5
# 
# Pré-traitement pour APN en taille originale
# et correction cosmétique automatique
# 
# 
# Ce script a besoin de 4 dossiers pour fonctionner
# à installer dans votre dossier de travail de Siril :
# offsets, darks, flats, brutes
#
############################################

#Création du master-offset
cd offsets
convertraw offset_
stack offset_ rej 3 3 -nonorm
cd ..

cd flats
#Pré-traitement des flats
convertraw flat_
preprocess flat_ -bias=../offsets/offset_stacked

#Création du master-flat
stack pp_flat_ rej 3 3 -norm=mul
cd ..

#Création du master-dark
cd darks
convertraw dark_
stack dark_ rej 3 3 -nonorm
cd ..

#pré-traitement des brutes
cd brutes
convertraw brute_
preprocess brute_ -dark=../darks/dark_stacked -flat=../flats/pp_flat_stacked -cfa -equalize_cfa

#Correction cosmétique automatique
seqfind_cosme_cfa pp_brute_ 3 3

#debayerisation des brutes
preprocess cc_pp_brute_ -debayer -stretch

#Alignement des brutes
register pp_cc_pp_brute_ 

#Empilement des brutes calibrées
stack r_pp_cc_pp_brute_ rej 3 3 -norm=addscale -out=../resultat

cd ..
close